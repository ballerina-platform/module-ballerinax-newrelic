/*
 * Copyright (c) 2023, WSO2 LLC. (https://www.wso2.com) All Rights Reserved.
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

description = 'Ballerina - New Relic Extension - Ballerina Module'

configurations {
    nativeJar {
        transitive false
    }
    externalJars {
        transitive false
    }
    distribution {
        canBeConsumed true
        canBeResolved false
    }
}

dependencies {
    nativeJar project(':newrelic-extension-native')

    externalJars "com.newrelic.telemetry:telemetry-all:${newrelicTelemetryVersion}"
    externalJars "com.newrelic.telemetry:telemetry-http-okhttp:${newrelicTelemetryVersion}"
    externalJars "com.squareup.okio:okio:${okioVersion}"
    externalJars "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    externalJars "org.jetbrains.kotlin:kotlin-runtime:${kotlinVersion}"
    externalJars "org.jetbrains.kotlin:kotlin-stdlib:${kotlinStdlibVersion}"
    externalJars "io.opentelemetry:opentelemetry-api:${openTelemetryVersion}"
    externalJars "io.opentelemetry:opentelemetry-api-metrics:${openTelemetryAlphaVersion}"
    externalJars "io.opentelemetry:opentelemetry-context:${openTelemetryVersion}"
    externalJars "io.opentelemetry:opentelemetry-sdk-trace:${openTelemetryVersion}"
    externalJars "io.opentelemetry:opentelemetry-sdk-common:${openTelemetryVersion}"
    externalJars "io.opentelemetry:opentelemetry-semconv:${openTelemetryAlphaVersion}"
    externalJars "io.opentelemetry:opentelemetry-proto:${openTelemetryAlphaVersion}"
    externalJars "io.opentelemetry:opentelemetry-exporter-otlp-trace:${openTelemetryVersion}"
    externalJars "io.opentelemetry:opentelemetry-exporter-otlp-common:${openTelemetryVersion}"
    externalJars "io.opentelemetry:opentelemetry-extension-trace-propagators:${openTelemetryVersion}"
    externalJars "io.grpc:grpc-api:${grpcVersion}"
    externalJars "io.grpc:grpc-context:${grpcVersion}"
    externalJars "io.grpc:grpc-core:${grpcVersion}"
    externalJars "io.grpc:grpc-stub:${grpcVersion}"
    externalJars "io.grpc:grpc-protobuf:${grpcVersion}"
    externalJars "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    externalJars "io.grpc:grpc-netty-shaded:${grpcVersion}"
    externalJars "io.perfmark:perfmark-api:${perfmarkVersion}"
    externalJars "com.google.guava:guava:${guavaVersion}"
    externalJars "com.google.guava:failureaccess:${failureAccessVersion}"
}

clean {
    delete "${project.projectDir}/target"
    delete "${project.projectDir}/Ballerina.lock"
    delete "${project.projectDir}/ballerina-internal.log"
}

jar {
    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
    }
}

task copyExternalJarsToLib(type: Copy) {
    into "${project.projectDir}/lib"
    from configurations.externalJars
}

def packageOrg = "ballerinax"
def packageName = "newrelic"
def ballerinaTomlFile = new File("${project.projectDir}/Ballerina.toml")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def artifactBallerinaDocs = file("${project.projectDir}/build/docs_parent/")
def artifactCacheParent = file("${project.buildDir}/cache_parent/")
def artifactLibParent = file("${project.buildDir}/lib_parent/")
def snapshotVersion = "-SNAPSHOT"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaCentralAccessToken = System.getenv('BALLERINA_CENTRAL_ACCESS_TOKEN')
def originalConfig = ballerinaTomlFilePlaceHolder.text
def artifactJar = file("$project.projectDir/target/cache/${packageOrg}/${packageName}/${tomlVersion}/java17/")
def platform = "java17"
def skipTests = false

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-');
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

task updateTomlVersions {
    doLast {
        def newrelicTelemetryVersion = project.newrelicTelemetryVersion
        def okioVersion = project.okioVersion
        def okhttpVersion = project.okhttpVersion
        def kotlinVersion = project.kotlinVersion
        def kotlinStdlibVersion = project.kotlinStdlibVersion
        def openTelemetryVersion = project.openTelemetryVersion
        def grpcVersion = project.grpcVersion
        def perfmarkVersion = project.perfmarkVersion
        def openTelemetryAlphaVersion = project.openTelemetryAlphaVersion
        def guavaVersion = project.guavaVersion
        def failureAccessVersion = project.failureAccessVersion

        def newConfig = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newConfig = newConfig.replace("@toml.version@", tomlVersion)
        newConfig = newConfig.replace("@newrelic.telemetry.version@", newrelicTelemetryVersion)
        newConfig = newConfig.replace("@okio.version@", okioVersion)
        newConfig = newConfig.replace("@okhttp.version@", okhttpVersion)
        newConfig = newConfig.replace("@kotlin.version@", kotlinVersion)
        newConfig = newConfig.replace("@kotlinStdlib.version@", kotlinStdlibVersion)
        newConfig = newConfig.replace("@opentelemetry.version@", openTelemetryVersion)
        newConfig = newConfig.replace("@openTelemetryAlpha.version@", openTelemetryAlphaVersion)
        newConfig = newConfig.replace("@grpc.version@", grpcVersion)
        newConfig = newConfig.replace("@perfmark.version@", perfmarkVersion)
        newConfig = newConfig.replace("@guava.version@", guavaVersion)
        newConfig = newConfig.replace("@failureAccess.version@", failureAccessVersion)
        ballerinaTomlFile.text = newConfig
    }
}

task revertTomlFile {
    doLast {
        ballerinaTomlFile.text = originalConfig
    }
}

task ballerinaBuild {
    dependsOn updateTomlVersions
    dependsOn configurations.nativeJar
    dependsOn configurations.externalJars
    dependsOn copyExternalJarsToLib
    dependsOn compileJava
    dependsOn compileTestJava
    dependsOn jar
    dependsOn checkstyleMain
    dependsOn checkstyleTest
    dependsOn spotbugsMain
    dependsOn spotbugsTest
    dependsOn test
    dependsOn ":newrelic-extension-ballerina:generatePomFileForMavenJavaPublication"

    inputs.dir file(project.projectDir)
    if (skipTests) {
        finalizedBy(revertTomlFile)
    }

    doLast {
        def additionalBuildParams = ""
        if (project.hasProperty("debug")) {
            additionalBuildParams = "--debug ${project.findProperty("debug")}"
        }

        // build bala file
        executeBalCommand ("build", "${project.projectDir}")
        executeBalCommand ("pack", "${project.projectDir}")

        // extract bala file to artifact cache directory
        file("${project.projectDir}/target/bala").eachFileMatch(~/.*.bala/) { balaFile ->
            copy {
                from zipTree(balaFile)
                into file("$artifactCacheParent/bala/${packageOrg}/${packageName}/${tomlVersion}/${platform}")
            }
        }
        copy {
            from file("${project.projectDir}/target/cache")
            exclude '**/*-testable.jar'
            exclude '**/tests_cache/'
            into file("$artifactCacheParent/cache/")
        }

        // Doc creation and packing
        executeBalCommand ("doc", "${project.projectDir}")

        copy {
            from file("$project.projectDir/target/apidocs/${packageName}")
            into file("$project.buildDir/docs_parent/docs/${packageName}")
        }
    }

    outputs.dir artifactCacheParent
    outputs.dir artifactBallerinaDocs
    outputs.dir artifactLibParent
}

task createArtifactZip(type: Zip) {
    destinationDirectory = file("${project.buildDir}/distributions")
    from ballerinaBuild
}

artifacts {
    distribution createArtifactZip
}

task ballerinaPublish {
    dependsOn updateTomlVersions
    dependsOn ballerinaBuild
    dependsOn ":newrelic-extension-ballerina:generatePomFileForMavenJavaPublication"

    finalizedBy(revertTomlFile)

    doLast {
        if (project.version.toString().split("-").length > 1) {
            return
        }
        if (ballerinaCentralAccessToken != null) {
            println("Publishing to the ballerina central...")
            def env = "JAVA_OPTS -DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            executeBalCommand ("push", "${project.projectDir}", env)
        } else {
            throw new InvalidUserDataException("Central Access Token is not present")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("packagePublishRepo")}")
            credentials {
                username = System.getenv("packageUser")
                password = System.getenv("packagePAT")
            }
        }
    }
}

build {
    dependsOn ballerinaBuild
}

publish {
    dependsOn ballerinaPublish
}

task extractBallerinaClassFiles(type: Copy) {
    fileTree(artifactJar).forEach { file ->
        from zipTree(file).matching {
            exclude '**/tests/*'
            include '**/*.class'
        }
        into "${project.rootDir.absolutePath}/build/classes"
    }
}

task publishBalaFileToLocal {
    dependsOn ballerinaBuild

    finalizedBy(revertTomlFile)

    doLast {
        if (!skipTests) {
            exec {
                workingDir "${project.rootDir}/ballerina"
                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    commandLine 'cmd', '/c', "bal.bat push --repository=local" +
                            " && exit %%ERRORLEVEL%%"
                } else {
                    commandLine 'sh', '-c', "bal push --repository=local"
                }
            }
        }
    }
}
